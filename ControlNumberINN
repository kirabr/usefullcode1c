&НаКлиенте
Процедура ПроверитьИНН(Команда)
	
	ИННКорректен = Ложь;
	
	Если СтрДлина(ИНН) = 10 Тогда
		
		КонтрольноеЧисло = РассчитатьКонтрольноеЧисло(Лев(ИНН, 9));;
		ИННКорректен = Сред(ИНН, 10) = КонтрольноеЧисло;
		
	ИначеЕсли СтрДлина(ИНН) = 12 Тогда
		
		КЧ1 = РассчитатьКонтрольноеЧисло(Лев(ИНН, 10));
		КЧ2 = РассчитатьКонтрольноеЧисло(СтрШаблон("%1%2", Лев(ИНН, 10), Формат(КЧ1)));
		КонтрольноеЧисло = СтрШаблон("%1%2", КЧ1, КЧ2);
		
		ИННКорректен = Сред(ИНН, 11) = КонтрольноеЧисло;
		
	Иначе
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "ИНН должен быть 10 или 12 символов.";
		Сообщение.Сообщить();
		
	КонецЕсли;
	
	Если ИННКорректен Тогда
		Элементы.ИНН.ЦветТекста = WebЦвета.Зеленый;
	Иначе
		Элементы.ИНН.ЦветТекста = WebЦвета.Красный;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция РассчитатьКонтрольноеЧисло(ЦифрыСтрокой)

	Множители = Новый Массив;
	Множители.Добавить(8);
	Множители.Добавить(6);
	Множители.Добавить(4);
	Множители.Добавить(9);
	Множители.Добавить(5);
	Множители.Добавить(3);
	Множители.Добавить(10);
	Множители.Добавить(4);
	Множители.Добавить(2);
	Множители.Добавить(7);
	Множители.Добавить(3);
	
	Цифры = Новый Массив;
	Для Сч=1 По СтрДлина(ЦифрыСтрокой) Цикл
		Цифры.Добавить(Число(Сред(ЦифрыСтрокой, СтрДлина(ЦифрыСтрокой)-Сч+1, 1 )));
	КонецЦикла;
	
	СуммаПроизведений = 0;
	Для Инд = 0 По Цифры.ВГраница() Цикл
		СуммаПроизведений = СуммаПроизведений + Цифры[Инд]*Множители[Инд];
	КонецЦикла;
	
	КЧ = СуммаПроизведений%11%10;
	
	Возврат Формат(КЧ, "ЧН=0");

КонецФункции // КонтрольноеЧисло()
